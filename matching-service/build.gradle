plugins {
    id 'java'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    jaxb
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation("jakarta.xml.ws:jakarta.xml.ws-api:4.0.2")
    implementation("com.sun.xml.ws:jaxws-rt:4.0.3")

    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.4'
    implementation 'wsdl4j:wsdl4j:1.6.3'

    implementation 'com.aerospike:aerospike-client:6.2.0'
    implementation 'org.apache.kafka:kafka-clients:3.6.1'

    jaxb 'org.glassfish.jaxb:jaxb-xjc:4.0.5'
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated-sources/jaxb"
        }
    }
}


tasks.register('genJaxb') {
    ext.sourcesDir = "$buildDir/generated-sources/jaxb"
    def schema = "src/main/resources/trip.xsd"

    outputs.dir sourcesDir

    doLast {
        ant.taskdef(name: 'xjc',
                classname: 'com.sun.tools.xjc.XJCTask',
                classpath: configurations.jaxb.asPath)

        ant.mkdir(dir: sourcesDir)

        ant.xjc(destdir: sourcesDir,
                schema: schema,
                package: 'com.example.trip') {
            arg(value: "-extension")
        }
    }
}

test {
    useJUnitPlatform()
}
